apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'


buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.triplet.gradle:play-publisher:1.1.5'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.github.triplet.play'

def version = "2.6.1-beta"

android {
    compileSdkVersion 27
    useLibrary 'org.apache.http.legacy'

    def travisBuildNumber = System.getenv("TRAVIS_BUILD_NUMBER")
    def buildNumber = (travisBuildNumber) ? (travisBuildNumber as int) : 1

    def applicationVersion = version
    def travisTag = System.getenv("TRAVIS_TAG")
    def buildVersionName = (travisTag) ? travisTag : applicationVersion + "-debug." + buildNumber

    defaultConfig {
        applicationId "org.openmrs.mobile"
        versionName buildVersionName
        versionCode buildNumber

        minSdkVersion 16
        targetSdkVersion 27

        testApplicationId "org.openmrs.mobile.test"
        testInstrumentationRunner "org.openmrs.mobile.test.OpenMRSInstrumentationTestRunner"
    }

    signingConfigs {
        release {
            storeFile rootProject.file('release.keystore')
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_ALIAS_NAME")
            keyPassword System.getenv("KEYSTORE_ALIAS_PASS")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        warning 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url 'http://yanzm.github.io/MaterialTabHost/repository' }
    mavenCentral()
    jcenter()
    google()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    def support_version = "27.1.1"
    
    implementation 'com.google.code.gson:gson:2.8.2'
    androidTestImplementation(
            'org.mockito:mockito-core:1.10.19',
            'com.google.dexmaker:dexmaker-mockito:1.2',
            'com.google.dexmaker:dexmaker:1.2',
            'com.jayway.android.robotium:robotium-solo:4.2'
    )

    androidTestImplementation files('libs/the-missing-android-xml-junit-test-runner-release-1.3_2.jar')
    implementation 'com.google.guava:guava:23.0'
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:gridlayout-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'commons-codec:commons-codec:1.10'
    implementation 'net.zetetic:android-database-sqlcipher:3.5.1@aar'
    implementation 'org.jdeferred:jdeferred-android-aar:1.2.4'
    implementation "com.android.support:support-v13:$support_version"
    implementation 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    implementation 'joda-time:joda-time:2.0'
    implementation 'net.sf.kxml:kxml2:2.3.0'
    implementation 'io.reactivex:rxjava:1.3.4'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.jakewharton.rxbinding:rxbinding:1.0.0'
    implementation 'com.github.hotchemi:permissionsdispatcher:2.3.1'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.3.1'
    implementation 'org.adw.library:discrete-seekbar:1.0.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'
    implementation 'org.mindrot:jbcrypt:0.4'
    testImplementation(
            'org.mockito:mockito-core:1.10.19',
            'junit:junit:4.12',
            'org.powermock:powermock-api-mockito:1.6.6',
            'org.powermock:powermock-module-junit4:1.6.6'
    )
}

play {
    if (version.contains("alpha")) {
        track = 'alpha'
    }
    else if (version.contains("beta")) {
        track = 'beta'
    }
    else {
        track = 'production'
    }

    serviceAccountEmail = System.getenv("PUBLISHER_ACCOUNT_ID")
    jsonFile = rootProject.file('google_play.json')
}
