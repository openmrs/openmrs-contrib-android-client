apply plugin: 'android'
apply plugin: 'pmd'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "org.openmrs.client"
        minSdkVersion 10
        targetSdkVersion 20
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

check.dependsOn 'pmd'
check.dependsOn 'checkstyle'

task pmd(type: Pmd) {
    ruleSetFiles = files("$projectDir/misc/pmd.xml")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reportsDir = file("$project.buildDir/reports/pmd")
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

task checkstyle(type: Checkstyle) {
    configFile file("$projectDir/misc/checkstyle.xml")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()

    reportsDir = file("$project.buildDir/reports/checkstyle")
    reports {
        xml.enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:20.+'
}
