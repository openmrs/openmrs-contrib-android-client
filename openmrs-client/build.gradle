apply plugin: 'com.android.application'
apply plugin: 'android-apt'

buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.triplet.gradle:play-publisher:1.1.5'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.github.triplet.play'
apply plugin: 'me.tatarka.retrolambda'

def version = "2.6.1-beta"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    useLibrary 'org.apache.http.legacy'

    def travisBuildNumber = System.getenv("TRAVIS_BUILD_NUMBER")
    def buildNumber = (travisBuildNumber) ? (travisBuildNumber as int) : 1

    def applicationVersion = version;
    def travisTag = System.getenv("TRAVIS_TAG")
    def buildVersionName = (travisTag) ? travisTag : applicationVersion + "-debug." + buildNumber

    defaultConfig {
        applicationId "org.openmrs.mobile"
        versionName buildVersionName
        versionCode buildNumber

        minSdkVersion 16
        targetSdkVersion 23

        testApplicationId "org.openmrs.mobile.test"
        testInstrumentationRunner "org.openmrs.mobile.test.OpenMRSInstrumentationTestRunner"
    }

    signingConfigs {
        release {
            storeFile rootProject.file('release.keystore')
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_ALIAS_NAME")
            keyPassword System.getenv("KEYSTORE_ALIAS_PASS")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        warning 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url 'http://yanzm.github.io/MaterialTabHost/repository' }
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}



dependencies {
    compile 'com.google.code.gson:gson:2.6.2'
    androidTestCompile(
            'org.mockito:mockito-core:1.10.19',
            'com.google.dexmaker:dexmaker-mockito:1.2',
            'com.google.dexmaker:dexmaker:1.2',
            'com.jayway.android.robotium:robotium-solo:4.2'
    )
    androidTestCompile files('libs/the-missing-android-xml-junit-test-runner-release-1.3_2.jar')
    compile 'com.google.guava:guava:18.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:gridlayout-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'commons-codec:commons-codec:1.10'
    compile 'org.apache.httpcomponents:httpcore:4.4.4'
    compile 'org.apache.httpcomponents:httpmime:4.5.2'
    compile 'net.yanzm:mth:1.0.4'
    compile 'net.zetetic:android-database-sqlcipher:3.5.1@aar'
    compile 'org.jdeferred:jdeferred-android-aar:1.2.4'
    compile 'com.android.support:support-v13:23.4.0'
    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    compile 'joda-time:joda-time:2.0'
    compile 'net.sf.kxml:kxml2:2.3.0'
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxbinding:rxbinding:1.0.0'
    compile 'com.github.hotchemi:permissionsdispatcher:2.3.1'
    apt 'com.github.hotchemi:permissionsdispatcher-processor:2.3.1'
    testCompile(
            'org.mockito:mockito-core:1.10.19',
            'junit:junit:4.12',
            'org.powermock:powermock-api-mockito:1.6.6',
            'org.powermock:powermock-module-junit4:1.6.6'
    )
}

play {
    if (version.contains("alpha")) {
        track = 'alpha'
    }
    else if (version.contains("beta")) {
        track = 'beta'
    }
    else {
        track = 'production'
    }

    serviceAccountEmail = System.getenv("PUBLISHER_ACCOUNT_ID")
    jsonFile = rootProject.file('google_play.json')
}
