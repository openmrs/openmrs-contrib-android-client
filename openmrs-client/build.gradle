apply plugin: 'android'
apply plugin: 'pmd'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 20
    buildToolsVersion '20.0.0'

    defaultConfig {
        applicationId "org.openmrs.client"
        minSdkVersion 10
        targetSdkVersion 20
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        quiet true
        checkReleaseBuilds false
    }
}

apply plugin: 'android-unit-test'
project.ext['src.java.dir'] = ['src/test/java']
project.ext['module.iml.file'] = "openmrs-client.iml"
apply from: 'https://raw.github.com/sababado/gradle-android-add-dir/master/gradle-android-add-dir.gradle'

tasks.whenTaskAdded { task ->
    if (task.name.equals("lint")) {
        task.enabled = false
    }
}

check.dependsOn 'pmd'
task pmd(type: Pmd) {
    ruleSetFiles = files("$projectDir/config/pmd/pmd.xml")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reportsDir = file("$project.buildDir/reports/pmd")
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

check.dependsOn 'checkstyle'
task checkstyle(type: Checkstyle) {
    configFile file("$projectDir/config/checkstyle/checkstyle.xml")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()

    reportsDir = file("$project.buildDir/reports/checkstyle")
    reports {
        xml.enabled = true
    }
}

dependencies {
    testCompile 'junit:junit:4.10'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:20.+'
    compile files('libs/volley.jar')
    compile files('libs/commons-codec.jar')
    compile files('libs/sqlcipher.jar')
    compile files('libs/guava-r09.jar')
}

repositories {
    mavenCentral()
}


