apply plugin: 'com.android.application'


buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.triplet.gradle:play-publisher:1.1.4'
    }
}

apply plugin: 'com.github.triplet.play'


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    useLibrary 'org.apache.http.legacy'

    def travisBuildNumber = System.getenv("TRAVIS_BUILD_NUMBER")
    def buildNumber = (travisBuildNumber) ? (travisBuildNumber as int) : 1

    def applicationVersion = "2.1"
    def travisTag = System.getenv("TRAVIS_TAG")
    def buildVersionName = (travisTag) ? travisTag : applicationVersion + "-debug." + buildNumber

    defaultConfig {
        applicationId "org.openmrs.mobile"
        versionName buildVersionName
        versionCode buildNumber

        minSdkVersion 11
        targetSdkVersion 22

        testApplicationId "org.openmrs.mobile.test"
        testInstrumentationRunner "org.openmrs.mobile.test.OpenMRSInstrumentationTestRunner"
    }

    signingConfigs {
        release {
            storeFile rootProject.file('release.keystore')
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_ALIAS_NAME")
            keyPassword System.getenv("KEYSTORE_ALIAS_PASS")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'

    }
}

repositories {
    maven { url 'http://yanzm.github.io/MaterialTabHost/repository' }
    mavenCentral()
    jcenter()
}



dependencies {
    compile project(':odkcollect')
    compile 'com.google.guava:guava:18.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:gridlayout-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
    compile 'commons-codec:commons-codec:1.10'
    compile 'org.apache.httpcomponents:httpcore:4.4.4'
    compile 'org.apache.httpcomponents:httpmime:4.5.2'
    compile 'net.yanzm:mth:1.0.4'
    compile 'net.zetetic:android-database-sqlcipher:3.4.0@aar'
    compile 'org.jdeferred:jdeferred-android-aar:1.2.4'

    androidTestCompile(
            'org.mockito:mockito-core:1.10.19',
            'com.google.dexmaker:dexmaker-mockito:1.0',
            'com.google.dexmaker:dexmaker:1.0',
            'com.jayway.android.robotium:robotium-solo:4.2'
    )
    androidTestCompile files('libs/the-missing-android-xml-junit-test-runner-release-1.3_2.jar')
}

play {
    track = 'alpha'
    serviceAccountEmail = System.getenv("PUBLISHER_ACCOUNT_ID")
    jsonFile = rootProject.file('google_play.json')

}